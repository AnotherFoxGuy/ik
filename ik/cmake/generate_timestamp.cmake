string (TIMESTAMP IK_BUILD_TIME UTC)

# Convert file contents into a CMake list (where each element in the list
# is one line of the file)
#
file (STRINGS "${IK_SOURCE_DIR}/cmake/build_number.txt" contents)
list (GET contents 1 IK_BUILD_NUMBER)

# update build number and save back
math (EXPR IK_BUILD_NUMBER "${IK_BUILD_NUMBER}+1")
file (WRITE "${IK_SOURCE_DIR}/cmake/build_number.txt" "# If you ever get merge conflicts with this file, figure out how many times you've built the file since the last successful pull and add that to the conflicting branch.\n")
file (APPEND "${IK_SOURCE_DIR}/cmake/build_number.txt" ${IK_BUILD_NUMBER})

macro (write_header)
    file(WRITE ${OUTPUT_FILE}
        "/* build_info_for_every_compile.h generated by CMake. Changes will be lost! */\n"
        "#define IK_BUILD_TIME \"${IK_BUILD_TIME}\"\n"
        "#define IK_BUILD_NUMBER ${IK_BUILD_NUMBER}\n")
endmacro ()

# If "build info for every compile" setting is false, only write a new header file but don't replace an existing one
if (NOT IK_UPDATE_INFO)
    if (NOT EXISTS ${OUTPUT_FILE})
        write_header()
    endif ()
else ()
    write_header()
endif ()
